workspace_root := justfile_directory()

_default:
    @just --list

# run rest api
api:
    @cargo run --package api --bin server --features api-doc

# watch rest api
api-dev:
    @cargo watch --clear --quiet --exec "run --package api --bin server --features api-doc"

# run angular spa
[working-directory("ui")]
ui-dev:
    pnpm start

# run api and spa
[parallel]
dev: api-dev ui-dev

# docker up
dup:
    @docker compose -f {{ workspace_root }}/docker/compose.yml up -d

# docker down
dwn:
    @docker compose -f {{ workspace_root }}/docker/compose.yml down

# dbmate rollback all
rollback-all:
    #!/usr/bin/env bash
    set -euo pipefail

    count=$(dbmate status | awk '/^Applied:/ { print $2 }')

    echo "Rolling back $count migration(s)..."

    for ((i = 0; i < count; i++)); do
        dbmate rollback
    done

# format rust code
api-fmt:
    @cargo +nightly fmt

# format typescript code
[working-directory("ui")]
ui-fmt:
    @pnpm run fmt

# format
[parallel]
fmt: api-fmt ui-fmt

# seed sample data
seeds key:
    @cargo run --package seeds --bin "{{ key }}"

# get keycloak test jwt token
get-token username password:
    @curl --silent --fail-with-body \
        -X POST \
        'http://localhost:8085/realms/blog-app/protocol/openid-connect/token' \
        -H 'Content-Type: application/x-www-form-urlencoded' \
        --data-urlencode 'client_id=blog-app-api' \
        --data-urlencode 'grant_type=password' \
        --data-urlencode 'username={{ username }}' \
        --data-urlencode 'password={{ password }}' \
        | jq -r '.access_token'
